//参考：https://raw.github.com/dm77/barcodescanner/master/build.gradle

buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://dl.google.com/dl/android/maven2/'
            name 'Google'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0'
        //第三方Lambda兼容插件，参阅 https://blog.csdn.net/ys743276112/article/details/51722047
        //classpath 'me.tatarka:gradle-retrolambda:3.2.5'
        //classpath 'me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
    }
}

allprojects {
    group = GROUP
    version = VERSION_NAME

    repositories {
        jcenter() //bintray的maven库
        mavenCentral() //sonatype的maven库
        mavenLocal() //本地的maven库
        maven {
            url 'https://dl.google.com/dl/android/maven2/' //谷歌的maven库
            name 'Google'
        }
        maven {
            url "https://www.jitpack.io" //jitpack的maven库
        }
        flatDir {
            dirs 'libs' //本地aar文件
        }
    }

    ext {
        isLibrary = false
        pomArtifactId = project.name
        pomVersion = VERSION_NAME
        pomDescription = 'This is library description'
    }

}

subprojects {
    afterEvaluate { Project project ->
        ext.pluginContainer = project.getPlugins()
        def hasAppPlugin = ext.pluginContainer.hasPlugin("com.android.application")
        def hasLibPlugin = ext.pluginContainer.hasPlugin("com.android.library")
        if (hasAppPlugin || hasLibPlugin) {
            configure(project) {
                apply from: "${rootDir}/common.gradle"
            }
        }
        if (project.isLibrary) {
            configure(project) {
                apply from: "${rootDir}/publish.gradle" //调用发布脚本
            }
        }
    }
}

